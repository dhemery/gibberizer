import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.junit.platform.console.options.Details

import static org.gradle.api.JavaVersion.VERSION_1_9

buildscript {
    ext {
        junitPlatformVersion = '1.0.2'
        tasktTreeVersion = '1.3'
    }

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'org.junit.platform', name: 'junit-platform-gradle-plugin', version: junitPlatformVersion
        classpath group: 'gradle.plugin.com.dorongold.plugins', name: 'task-tree', version: tasktTreeVersion
    }
}


ext {
    vendor = 'Dale H. Emery'
    apiGuardianVersion = '1.0.0'
    googleTruthVersion = '0.37'
    junitJupiterVersion = '5.0.2'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.dorongold.task-tree'
apply plugin: 'org.junit.platform.gradle.plugin'

group = 'com.dhemery'
description = 'Automated gibberish'
version = '1.0.0'
mainClassName = 'com.dhemery.gibberizer.application.GibberizerApplication'

targetCompatibility = sourceCompatibility = VERSION_1_9

repositories {
    jcenter()
}

dependencies {
    testCompileOnly group: 'org.apiguardian', name: 'apiguardian-api', version: apiGuardianVersion

    testCompile group: 'com.google.truth', name: 'truth', version: googleTruthVersion
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

jar {
    manifest {
        attributes(
                'Implementation-Title': description,
                'Implementation-Version': version,
                'Implementation-Vendor': vendor,
                'Main-Class': mainClassName
        )
    }
}

javadoc {
    options.links << 'https://docs.oracle.com/javase/9/docs/api/'
}

junitPlatform {
    details = Details.TREE
}

test {
    testLogging {
        exceptionFormat = TestExceptionFormat.FULL
    }
}

artifacts {
    archives sourcesJar, javadocJar
}

defaultTasks clean.name, build.name
